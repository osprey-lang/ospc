use namespace aves;

namespace osprey.compiler.syntax.wrapped;

/// Summary: Implements an immutable list of {WrappedParameter} declarations, which
///          represent parameters of a function member (such as a method, indexer or
///          constructor). This collection is more specialised than {WrappedNodeList},
///          containing members that simplify the manipulation of parameter lists.
public inheritable WrappedParameterList is WrappedNodeList
{
	public new()
	{
		new this([], 0, VariadicParameter.none, 0);
	}
	public new(items)
	{
		new base(items);
	}
	public new(items, optionalParameterCount, variadicParameter, refParameterCount)
	{
		new base(items);

		_optionalParameterCount = optionalParameterCount;
		_variadicParameter      = variadicParameter;
		_refParameterCount      = refParameterCount;
	}

	private _optionalParameterCount;
	/// Summary: Gets the total number of optional parameters in this parameter list.
	public get optionalParameterCount
	{
		if _optionalParameterCount is null:
			_optionalParameterCount = count(@.isOptional);
		return _optionalParameterCount;
	}

	/// Summary: Determines whether the parameter list has any optional parameters.
	public get hasOptionalParameters = optionalParameterCount > 0;

	private _variadicParameter;
	/// Summary: Gets a {VariadicParameter} value that indicates which parameter, if
	///          any, is variadic.
	public get variadicParameter
	{
		if _variadicParameter is null
		{
			var param = VariadicParameter.none;
			if length > 0
			{
				if collection[-1].isVariadic:
					param = VariadicParameter.last;
				else if collection[0].isVariadic:
					param = VariadicParameter.first;
			}
			_variadicParameter = param;
		}
		return _variadicParameter;
	}

	private _refParameterCount;
	/// Summary: Gets the total number of ref parameters in this parameter list.
	public get refParameterCount
	{
		if _refParameterCount is null:
			_refParameterCount = count(@.isRef);
		return _refParameterCount;
	}

	/// Summary: Determines whether the parameter list has any ref parameters.
	public get hasRefParameters = refParameterCount > 0;

	private static _emptyList = new WrappedParameterList();

	/// Summary: Gets an empty {WrappedParameterList}.
	/// Returns: A {WrappedParameterList} with a length of 0.
	public static get emptyList = _emptyList;
}
