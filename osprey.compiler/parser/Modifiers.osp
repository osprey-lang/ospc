use namespace aves;
use namespace osprey.compiler.syntax;

namespace osprey.compiler.parser;

/// Summary: Represents zero or more member modifiers that were consumed during parsing.
///          Instances of this class are intended to be reused if modifiers need to be
///          parsed multiple times, to avoid repeated memory allocations.
///
///          The {Parser.parseModifiers} method is used for parsing modifiers.
public class Modifiers
{
	/// Summary: A {NodeList} containing the {Annotation} instances associated
	///          with the {Modifiers}.
	public annotations = EmptyLists.node;

	/// Summary: The first modifier {Token}, or null if this instance has no modifiers.
	public start;

	/// Summary: The `public` modifier {Token} if specified, otherwise null.
	public \public;
	/// Summary: The `protected` modifier {Token} if specified, otherwise null.
	public \protected;
	/// Summary: The `private` modifier {Token} if specified, otherwise null.
	public \private;
	/// Summary: The `static` modifier {Token} if specified, otherwise null.
	public \static;
	/// Summary: The `abstract` modifier {Token} if specified, otherwise null.
	public \abstract;
	/// Summary: The `inheritable` modifier {Token} if specified, otherwise null.
	public \inheritable;
	/// Summary: The `overridable` modifier {Token} if specified, otherwise null.
	public \overridable;
	/// Summary: The `override` modifier {Token} if specified, otherwise null.
	public \override;

	/// Summary: Determines whether this {Modifiers} instance has no modifiers
	///          and no annotations.
	public get isEmpty = not hasModifiers and annotations.isEmpty;

	/// Summary: Determines whether this {Modifiers} instance has any modifiers.
	public get hasModifiers =
		\public      or
		\protected   or
		\private     or
		\static      or
		\abstract    or
		\inheritable or
		\overridable or
		\override;

	/// Summary: Determines whether this {Modifiers} instance has an accessibility modifier.
	public get hasAccessibility = \public or \protected or \private;

	/// Summary: Gets an {Accessibility} value representing the declared accessibility
	///          of the member. If no accessibility modifier is present, the return
	///          value is always {Accessibility.none}.
	public get accessibility
	{
		if \public:
			return Accessibility.\public;
		if \protected:
			return Accessibility.\protected;
		if \private:
			return Accessibility.\private;
		return Accessibility.none;
	}

	/// Summary: Removes all modifiers from this instance.
	public clear()
	{
		annotations  = EmptyLists.node;
		start        = null;
		\public      = null;
		\protected   = null;
		\private     = null;
		\static      = null;
		\abstract    = null;
		\inheritable = null;
		\overridable = null;
		\override    = null;
	}

	/// Summary: Verifies the modifiers for a global function or constant declaration.
	///          If any modifiers are invalid, a recoverable error is triggered on the
	///          specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForGlobalFunctionOrConstant(parser)
	{
		var invalidToken = \protected ??
			\static ??
			\abstract ?? \inheritable ??
			\overridable ?? \override;
		if invalidToken:
			parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForGlobalFunctionOrConstant);
	}

	/// Summary: Verifies the modifiers for a class declaration. If any modifiers are
	///          invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForClass(parser)
	{
		var invalidToken = \protected ??
			\overridable ?? \override;
		if invalidToken:
			parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForClass);

		if \static
		{
			if \abstract or \inheritable:
				parser.recoverableError(\abstract ?? \inheritable, ErrorCode.err_StaticClassCannotBeAbstractOrInheritable);
		}
		else if \abstract and \inheritable:
			parser.recoverableError(\abstract, ErrorCode.err_ClassCannotBeBothAbstractAndInheritable);
	}

	/// Summary: Verifies the modifiers for a constructor declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForConstructor(parser)
	{
		if \static
		{
			var invalidToken =
				\public ?? \protected ?? \private ??
				\abstract ?? \inheritable ??
				\overridable ?? \override;
			if invalidToken:
				parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForStaticConstructor);
		}
		else
		{
			var invalidToken =
				\abstract ?? \inheritable ??
				\overridable ?? \override;
			if invalidToken:
				parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForConstructor);
		}
	}

	/// Summary: Verifies the modifiers for a field declaration. If any modifiers are
	///          invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForField(parser)
	{
		var invalidToken =
			\abstract ?? \inheritable ??
			\overridable ?? \override;
		if invalidToken:
			parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForField);
	}

	/// Summary: Verifies the modifiers for a method declaration. If any modifiers are
	///          invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForMethod(parser)
	{
		if \static
		{
			var invalidToken =
				\abstract ?? \inheritable ??
				\overridable ?? \override;
			if invalidToken:
				parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForStaticMethod);
		}
		else
		{
			if \inheritable:
				parser.recoverableError(\inheritable, ErrorCode.err_MethodCannotBeInheritable);

			if \abstract and \overridable:
				parser.recoverableError(\overridable, ErrorCode.err_AbstractMethodCannotBeOverridable);
		}
	}

	/// Summary: Verifies the modifiers for an invocator declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForInvocator(parser)
	{
		if \static:
			parser.recoverableError(\static, ErrorCode.err_InvocatorCannotBeStatic);
		verifyForMethod(parser);
	}

	/// Summary: Verifies the modifiers for a property declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForProperty(parser)
	{
		if \static
		{
			var invalidToken =
				\abstract ?? \inheritable ??
				\overridable ?? \override;
			if invalidToken:
				parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForStaticProperty);
		}
		else
		{
			if \inheritable:
				parser.recoverableError(\inheritable, ErrorCode.err_PropertyCannotBeInheritable);

			if \abstract and \overridable:
				parser.recoverableError(\overridable, ErrorCode.err_AbstractPropertyCannotBeOverridable);
		}
	}

	/// Summary: Verifies the modifiers for an indexer declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForIndexer(parser)
	{
		if \static:
			parser.recoverableError(\static, ErrorCode.err_IndexerCannotBeStatic);
		verifyForProperty(parser);
	}

	/// Summary: Verifies the modifiers for a class constant declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForClassConstant(parser)
	{
		var invalidToken =
			\static ??
			\abstract ?? \inheritable ??
			\overridable ?? \override;
		if invalidToken:
			parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForClassConstant);
	}

	/// Summary: Verifies the modifiers for an operator declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForOperator(parser)
	{
		if hasModifiers:
			parser.recoverableError(start, ErrorCode.err_InvalidModifierForOperator);
	}

	/// Summary: Verifies the modifiers for an iterator declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForIterator(parser)
	{
		if hasModifiers:
			parser.recoverableError(start, ErrorCode.err_InvalidModifierForIterator);
	}

	/// Summary: Verifies the modifiers for an enum declaration. If any modifiers are
	///          invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForEnum(parser)
	{
		var invalidToken = \protected ??
			\static ??
			\abstract ?? \inheritable ??
			\overridable ?? \override;
		if invalidToken:
			parser.recoverableError(invalidToken, ErrorCode.err_InvalidModifierForEnum);
	}

	/// Summary: Verifies the modifiers for a namespace declaration. If any modifiers
	///          are invalid, a recoverable error is triggered on the specified parser.
	/// Param parser: The {Parser} that receives modifier errors.
	public verifyForNamespace(parser)
	{
		if not isEmpty:
			parser.recoverableError(start, ErrorCode.err_InvalidNamespaceModifiers);
	}
}
