use aves;

namespace osprey.compiler.syntax;

public class Document is ParseNode
{
	public new(location, useDirectives, globalAnnotations, fileNamespaceName, members, statements)
	{
		new base(location);

		this._useDirectives     = claimAll(useDirectives ?? EmptyLists.node);
		this._globalAnnotations = claimAll(globalAnnotations ?? EmptyLists.node);
		this._fileNamespaceName = fileNamespaceName;
		this._members           = claimAll(members ?? EmptyLists.node);
		this._statements        = claimAll(statements ?? EmptyLists.node);
	}

	private _useDirectives;
	/// Summary: Gets a {NodeList} containing the {UseDirective}s in
	///          this compilation unit.
	public get useDirectives = _useDirectives;

	private _globalAnnotations;
	/// Summary: Gets a {NodeList} containing the {Annotation}s targeted
	///          at the module.
	public get globalAnnotations = _globalAnnotations;

	private _fileNamespaceName;
	/// Summary: Gets a {QualifiedName} containing the file namespace
	///          name of this compilation unit.
	public get fileNamespaceName = _fileNamespaceName;

	private _members;
	/// Summary: Gets a {NodeList} containing the members declared in
	///          the compilation unit, not including global variables.
	public get members = _members;

	private _statements;
	/// Summary: Gets a {NodeList} containing the global {Statement}s
	///          of this compilation unit, including global variable
	///          declarations.
	public get statements = _statements;

	override accept(visitor, arg)
	{
		return visitor.visitDocument(this, arg);
	}
}
