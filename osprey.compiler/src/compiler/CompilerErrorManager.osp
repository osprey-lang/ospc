use aves.*;

namespace osprey.compiler;

public class CompilerErrorManager is ErrorManager
{
	public new(printErrors, printWarnings)
	{
		this.printErrors = bool(printErrors);
		this.printWarnings = bool(printWarnings);
	}

	private printErrors = true;
	private printWarnings = true;

	private errors = [];
	private warnings = [];

	override get hasErrors => not errors.isEmpty;

	override get hasWarnings => not warnings.isEmpty;

	private printMessage(kind, message)
	{
		var code = message.errorCode.toString("D4");
		if message.source is not null {
			var loc = message.source;
			var col;
			var line = loc.getLineNumber(1, ref col);
			print(messageFormat.format([
				kind,
				loc.sourceFile.fileName,
				line,
				col,
				loc.endIndex - loc.startIndex,
				code,
				message.message
			]));
		}
		else {
			print(messageFormatWithoutSource.format([code, message.message]));
		}
	}

	override addErrorInner(error)
	{
		errors.add(error);
		if printErrors {
			printMessage("error", error);
		}
	}

	override getAllErrors()
	{
		return errors.slice(0);
	}

	override clearErrors()
	{
		errors.clear();
	}

	override addWarningInner(warning)
	{
		warnings.add(warning);
		if printWarnings {
			printMessage("warn", warning);
		}
	}

	override getAllWarnings()
	{
		return warnings.slice(0);
	}

	override clearWarnings()
	{
		warnings.clear();
	}

	private const messageFormat = "[{0} \"{1}\":{2}:{3}+{4}] OSP{5}: {6}";
	private const messageFormatWithoutSource = "[{0}] OSP{1}: {2}";
}
